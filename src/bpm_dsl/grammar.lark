// BPM DSL Grammar for Lark parser

?start: process

process: "process" STRING "{" process_body "}"

process_body: process_metadata* element* flow_section?

process_metadata: process_id
                | process_version

process_id: "id:" STRING
process_version: "version:" STRING

element: start_element
       | end_element  
       | script_call
       | xor_gateway

start_element: "start" STRING "{" start_properties "}"
start_properties: element_id

end_element: "end" STRING "{" end_properties "}"
end_properties: element_id

script_call: "scriptCall" STRING "{" script_properties "}"
script_properties: element_id script_code (input_mappings | input_vars)? (output_mappings | output_vars)? result_variable?

xor_gateway: "xorGateway" STRING "{" gateway_properties "}"
gateway_properties: element_id gateway_condition?

element_id: "id:" STRING
script_code: "script:" STRING
input_mappings: "inputMappings:" mapping_array
output_mappings: "outputMappings:" mapping_array
input_vars: "inputVars:" string_array
output_vars: "outputVars:" string_array
result_variable: "resultVariable:" STRING
gateway_condition: "when:" STRING

flow_section: "flow" "{" flow_definition* "}"
flow_definition: STRING "->" STRING flow_condition?
flow_condition: "[" "when:" STRING "]"

string_array: "[" (STRING ("," STRING)*)? "]"
mapping_array: "[" (variable_mapping ("," variable_mapping)*)? "]"
variable_mapping: "{" mapping_source mapping_target "}"
mapping_source: "source:" STRING
mapping_target: "target:" STRING

// Terminals
STRING: /"[^"]*"/
COMMENT: /\/\/[^\n]*/

%import common.WS
%ignore WS
%ignore COMMENT
