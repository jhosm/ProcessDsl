// BPM DSL Grammar for Lark parser

?start: process

process: "process" STRING "{" process_body "}"

process_body: process_metadata* element* flow_section?

process_metadata: process_id
                | process_version

process_id: "id:" STRING
process_version: "version:" STRING

element: start_element
       | end_element  
       | script_call
       | service_task
       | process_entity
       | xor_gateway

start_element: "start" STRING "{" start_properties "}"
start_properties: element_id?

end_element: "end" STRING "{" end_properties "}"
end_properties: element_id?

script_call: "scriptCall" STRING "{" script_properties "}"
script_properties: element_id? script_code? (input_mappings | input_vars)? (output_mappings | output_vars)? result_variable?

service_task: "serviceTask" STRING "{" service_properties "}"
service_properties: element_id? task_type? task_retries? task_headers? (input_mappings | input_vars)? (output_mappings | output_vars)?

process_entity: "processEntity" STRING "{" process_entity_properties "}"
process_entity_properties: entity_model? entity_name?

xor_gateway: "xorGateway" STRING "{" gateway_properties "}"
gateway_properties: element_id? gateway_condition?

element_id: "id:" STRING
script_code: "script:" STRING
input_mappings: "inputMappings:" mapping_array
output_mappings: "outputMappings:" mapping_array
input_vars: "inputVars:" string_array
output_vars: "outputVars:" string_array
result_variable: "resultVariable:" STRING
task_type: "type:" STRING
task_retries: "retries:" NUMBER
task_headers: "headers:" header_array
entity_type: "type:" STRING
entity_model: "entityModel:" STRING
entity_name: "entityName:" STRING
gateway_condition: "when:" STRING

flow_section: "flow" "{" flow_definition* "}"
flow_definition: STRING "->" STRING flow_condition?
flow_condition: "[" "when:" STRING "]" | "[" DEFAULT "]"

string_array: "[" (STRING ("," STRING)*)? "]"
mapping_array: "[" (variable_mapping ("," variable_mapping)*)? "]"
header_array: "[" (task_header ("," task_header)*)? "]"
variable_mapping: STRING "->" STRING
task_header: STRING "->" STRING

// Terminals
STRING: /"[^"]*"/
NUMBER: /\d+/
DEFAULT: "default"
COMMENT: /\/\/[^\n]*/

%import common.WS
%ignore WS
%ignore COMMENT
